SCENE 6700, "athalantisoutside", SCENE, "Port - if you can call that a port..."
#include include.txt
ACTOR 1, "Sailor", human63, 1, 25, 90
IF T3993|T3988 @halloweenActors
GOTO @nextStepAfterHalloweenActors

@halloweenActors
ACTOR 1, "Sailor", #<spEvent.Name>-humans, 63, 25, 90
@nextStepAfterHalloweenActors
IF Z259+Z206.30 @seasigerhair
1: Hello, adventurer!
1: So you want to sail, eh!
1: That's fine!
1: Where do you want to go?
WAIT 1
@menu
SOUND sword1.wav
MENU "Athalantis=@athalantis", "Armenis=@armenis", "Nowhere, I am here to buy some potions=@nowhere"
GOTO @menu

@nowhere
SOUND sword1.wav
1: Please, have a look.
OFFER 1, 3, 4, 7, 13, 29, 30, 31, 32, 33, 36, 38, 39
END

@seasigerhair
1: Oh my god!
1: You've got Seasinger Queen Hair!
1: Listen: I exchange them and the seasinger air you have for this magical dagger.
1: Sounds good?
@seasigerhairask
ASK 60
IF YES @seasigerhairYES
IF NO @seasigerhairNO
1: What?
GOTO @seasigerhairask

@seasigerhairNO
1: Maybe you'll change your mind.
1: Do you want to go somewhere by the way?
GOTO @menu

@seasigerhairYES
1: Great!
1: Here is the dagger!
SOUND found Item.wav
HOST_TAKE Z259
HOST_TAKE Z206.30
HOST_GIVE I1145
END

@bah
1: Bah
1: Come back later if you change your mind...
END

@athalantis
SOUND sword1.wav
1: Athalantis... 
1: 10 000 #<GP.name> : sounds good?
@athask
ASK 30
IF YES @travath
IF NO @bah
1: Excuse me?
GOTO @athask

@travath
IF -G10000 @poor
TAKE G10000
1: Here you go.
IF R1+R30 @randomE
GOTO LINK 41, 1
END

@armenis
SOUND sword1.wav
1: Armenis, eh!
1: Good, very good!
1: What about 10 000 #<GP.name> to go there?
@travarm
ASK 30
IF YES @goarm
IF NO @bah
1: What?
GOTO @travarm

@goarm
IF -G10000 @poor
TAKE G10000
1: Here you go.
IF R1+R30 @randomE
GOTO LINK 42, 1
END

@randomE
#include quests\piratehat.txt
END

@poor
1: Eh, poor one, I don't work for free!
1: Come back when you have some cash.
END
;
;---------------------------------------------------------------------------------------------------------
;
SCENE 6701, castle, SCENE, "Gate"
#include include.txt
GOTO LINK 43, 0
;
;---------------------------------------------------------------------------------------------------------
;
